import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.services.extended.*;
import com.rameses.util.*


class ResectionSupportService
{
    @Env
    def env
    
    @Service('DateService')
    def dtSvc

    @Service('ResectionService')
    def resectionSvc

    @Service('ExaminationService')
    def examinationSvc

    @Service('RPTRequirementService')
    def reqSvc 

    @Service('FAASService')
    def faasSvc

    @Service('DBImageService')
    def imageSvc

    @Service('EntityService')
    def entitySvc 

    @Service('TxnRefService')
    def txnRef

    @Service('NotificationService')
    def notifySvc   


    @DataContext('resection')
    def em_resection 

    @DataContext('resection_item')
    def em_resection_item

    @DataContext('resection_task')
    def em_resection_task 

    @DataContext('faas')
    def em_faas 

    @DataContext('faas_task')
    def em_faas_task


    @ProxyMethod
    public def buildData(resection){
        resection.putAll(resectionSvc.open(resection))
        resection.items = em_resection_item.find([parent_objid: resection.objid]).list();
        resection.tasks = em_resection_task.where("refid = :objid and enddate is not null and state not like 'assign%'", resection).list()
        resection.image = getImageData(resection.objid)
        return resection
    }


    @ProxyMethod
    public def buildExaminations(resection){
        def data = [:]
        data.images = []
        data.examinations = examinationSvc.getFindings( resection.objid ).each{
            it.putAll(examinationSvc.open(it))
            data.images << getImageData(it.objid)
        }
        return data 
    }


    @ProxyMethod
    public def getRequirements(resection){
        return reqSvc.getRequirements(resection.objid)
    }
    
    @ProxyMethod
    public def getApprovedFaas(faas){
        faas = faasSvc.openFaas(faas)
        faas.taxpayer = entitySvc.open(faas.taxpayer)
        faas.tasks = em_faas_task.where('refid = :refid AND enddate IS NOT NULL', [refid: faas.objid]).list()
        return faas

    }

    @ProxyMethod
    public def buildRequirement(requirement){
        def data = [:]
        data.requirement = requirement 
        data.image = getImageData(requirement.objid)
        return data 
    }


    @ProxyMethod
    public def buildFaasData(item){
        def faas = faasSvc.openFaas([objid:item.newfaas.objid])
        faas.taxpayer = entitySvc.open(faas.taxpayer)
        if (item._includetask == null || item._includetask == true){
            faas.tasks = em_faas_task.where("refid = :objid and enddate is not null and state not like 'assign%'", faas).list()
        }
        if (item._includeimage == null || item._includeimage == true){
            faas.image = getImageData(faas.objid)
        }
        return faas 
    }    


    @ProxyMethod
    public def postData(resection){
        def oldresection = em_resection.find([objid:resection.objid]).first()
        if (!resection._resubmitted || !oldresection){
            txnRef.delete(resection.objid)
            em_resection.save(resection)
        }

        resection.items.each{
            txnRef.delete(it.newfaas.objid)
            em_resection_item.save(it)
            txnRef.insertRef(it.newfaas.objid, resection.objid, 'FAAS is currently referenced by Resection  No. ' + resection.txnno +'.')
        }

        resection.tasks.each{
            em_resection_task.save(it)
        }

        saveImageData(resection.image)
    }


    @ProxyMethod
    public void postExaminations(data) {
        data.examinations.each{
            examinationSvc.save(it)
        }

        data.images.each{
            saveImageData(it)
        }       
    }


    @ProxyMethod
    public void postRequirements(data) {
        data.requirements.each{
            reqSvc.save(it)
        }

        data.images.each{
            saveImageData(it)
        }       
    }

    @ProxyMethod
    public void postRequirement(data) {
        reqSvc.save(data.requirement)
        saveImageData(data.image)
    }


    @ProxyMethod
    public void postFaasData(faas) {
        def oldfaas = em_faas.select('state').find([objid:faas.objid]).first()
        if (oldfaas?.state != 'CURRENT'){
            //old faas is not yet approved, 
            //set province faas state to pending 
            faas.state = 'PENDING'
        }

        entitySvc.save(faas.taxpayer)

        faas.taxpayer.address = faas.taxpayer.address?.text
        faas._checkTxnRef = false;

        if (faasSvc.faasExist(faas))
            faasSvc.updateFaas(faas)
        else
            faasSvc.createFaas(faas)

        faas.tasks.each{
            em_faas_task.save(it)
        }

        saveImageData(faas.image)
    }

    @ProxyMethod
    public def postApprovedFaas(faas){
        def currfaas = em_faas.select('state').find([objid:faas.objid]).first()
        if (currfaas && currfaas.state == 'CURRENT') {
            saveFaasTask(faas)
            return
        }
        faas.state = 'PENDING'
        postFaasData(faas)
        faasSvc.approveFaas(faas)
        saveFaasTask(faas)
    }

    @ProxyMethod
    public def postApprovedResection(resection){
        def state = em_resection.select("state").find([objid: resection.objid]).val();
        if (!state) {
            postData(resection)
        } else if (! state.equalsIgnoreCase('APPROVED')){
            resection.tasks.each{
                em_resection_task.save(it)
            }
            em_resection_task.where('refid = :objid and enddate is null', resection).delete()
            resectionSvc.insertFaasSignatories(resection)
            resectionSvc.approve(resection)

            notifyRecommender(resection)
        } 
    }

    @ProxyMethod
    public def postDisapprovedResection(resection) {
    }

    void saveImageData(image){
        if (!image) return
        
        image.headers.each{
            imageSvc.saveHeader(it)
        }
        image.chunks.each{
            imageSvc.saveItem(it)
        }
    }


    def getImageData(objid){
        def headers = imageSvc.getImages([refid:objid])
        def chunks = []
        headers.each{
            chunks += imageSvc.getImageItems(it)
        }       
        return [headers:headers, chunks:chunks]
    }

    void notifyRecommender(resection){
        def recommendertask = em_resection_task.where('refid = :objid and state = :state', [objid:resection.objid, state:'recommender'])
                                        .orderBy('enddate desc')
                                        .first()

        def approvertask = resection.tasks.find{it.state.matches('approver|provapprover')}
        if (recommendertask && approvertask){
            def sendername = (approvertask.actor.name ? approvertask.actor.name : 'Province')
            def msg = [
                objid     : recommendertask.refid,
                recipientid : (recommendertask.assignee.objid ? recommendertask.assignee.objid : '-'),
                recipienttype : 'user',
                senderid  : '-',
                sender    : sendername,
                message   : 'Resection ' + resection.txnno + ' has already been approved by ' + sendername + '.',
                filetype  : 'resection',
                data      : [:],
            ]
            notifySvc.addMessage(msg)
        }       
    }

    void saveFaasTask(faas) {
        faas.tasks.each{
            em_faas_task.save(it)
        }
    }

}


